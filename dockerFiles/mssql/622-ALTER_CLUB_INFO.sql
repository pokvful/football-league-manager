CREATE OR ALTER PROCEDURE ADD_PLAYER_CLUB
(@first_name NAME,
@last_name NAME,
@birth_date DATE,
@nationality COUNTRY_NAME,
@club_name CLUB_NAME,
@jersey JERSEY_NUMBER,
@middle_name NAME = NULL)
AS
BEGIN
	SET NOCOUNT ON
	BEGIN TRY
	DECLARE @person_id TABLE (id PERSON_ID) 

	INSERT INTO PERSON (FIRST_NAME, LAST_NAME, MIDDLE_NAME, BIRTH_DATE, COUNTRY_NAME)
	OUTPUT inserted.PERSON_ID
		INTO @person_id
	VALUES (@first_name, @last_name, @middle_name, @birth_date, @nationality)

	INSERT INTO PLAYER (PERSON_ID, CLUB_NAME, JERSEY)
	VALUES ((SELECT id FROM @person_id) , @club_name, @jersey)
	END TRY
	BEGIN CATCH
		THROW
	END CATCH
END
GO
CREATE OR ALTER PROCEDURE ALTER_PLAYER_CLUB
(@person_id PERSON_ID,
@new_club CLUB_NAME,
@jersey JERSEY_NUMBER = NULL)
AS
BEGIN
	SET NOCOUNT ON
	BEGIN TRY
	UPDATE PLAYER
	SET CLUB_NAME = @new_club,
		JERSEY = CASE WHEN (@jersey IS NULL) THEN JERSEY ELSE @jersey END
	WHERE PERSON_ID = @person_id
	END TRY
	BEGIN CATCH
		THROW
	END CATCH
END
GO
CREATE OR ALTER PROCEDURE UPDATE_CLUB
(@CLUB_NAME CLUB_NAME,
@STADIUM_NAME STADIUM_NAME = NULL,
@CAPACITY CAPACITY = NULL,
@COUNTRY_NAME COUNTRY_NAME = NULL,
@CITY_NAME CITY_NAME = NULL,
@coach PERSON_ID = null,
@NEW_CLUB_NAME CLUB_NAME = null)
AS
BEGIN
	SET NOCOUNT ON
	BEGIN TRY
	IF @COUNTRY_NAME IS NOT NULL OR @CITY_NAME IS NOT NULL OR @coach IS NOT NULL
		UPDATE CLUB
		SET COUNTRY_NAME = CASE WHEN (@COUNTRY_NAME IS NULL) THEN COUNTRY_NAME ELSE @COUNTRY_NAME END,
			CITY_NAME = CASE WHEN (@CITY_NAME IS NULL) THEN CITY_NAME ELSE @CITY_NAME END,
			COACH_PERSON_ID = CASE WHEN (@coach IS NULL) THEN COACH_PERSON_ID ELSE @coach END
		WHERE CLUB_NAME = @CLUB_NAME
	
	IF @STADIUM_NAME IS NOT NULL
		BEGIN
			IF NOT EXISTS (SELECT 1 FROM STADIUM WHERE STADIUM_NAME = @STADIUM_NAME)
				INSERT INTO STADIUM (STADIUM_NAME, CAPACITY) VALUES
				(@STADIUM_NAME, @CAPACITY)

			UPDATE CLUB
			SET STADIUM_NAME = @STADIUM_NAME
			WHERE CLUB_NAME = @CLUB_NAME
		END

	IF @NEW_CLUB_NAME IS NOT NULL
		UPDATE CLUB
		SET CLUB_NAME = @NEW_CLUB_NAME
		WHERE CLUB_NAME = @CLUB_NAME
	END TRY
	BEGIN CATCH
		THROW
	END CATCH
END